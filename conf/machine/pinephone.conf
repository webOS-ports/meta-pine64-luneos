#@TYPE: Machine
#@NAME: Pinephone "Don't be evil" devkit board
#@DESCRIPTION: An Allwinner based development board http://wiki.pine64.org/index.php/Project_Don't_be_evil

require conf/machine/include/arm/arch-arm64.inc

PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot"

EXTRA_IMAGEDEPENDS += "u-boot"
UBOOT_MACHINE ?= "pinephone_defconfig"
UBOOT_BINARY ?= "u-boot-sunxi-with-spl.bin"
UBOOT_ENV ?= "boot"
UBOOT_ENV_SUFFIX ?= "scr"
SPL_BINARY ?= "spl/sunxi-spl.bin"

MACHINE_EXTRA_RRECOMMENDS += "kernel-modules"
MACHINE_EXTRA_RDEPENDS = " \
    linux-firmware-pine64 \
    kernel-modules \
    \
    qtsensors-sensorfw-plugin \
    sensorfw \
    \
    eg25-manager \
"

IMAGE_FSTYPES += "wic.gz wic.bmap"
WKS_FILE = "pinephone-luneos-bsp-image.wks"
WKS_FILE_DEPENDS ?= " \
    e2fsprogs-native \
    mtools-native \
    dosfstools-native \
    virtual/bootloader \
    virtual/kernel \
    initramfs-uboot-image \
"
IMAGE_BOOT_FILES = " \
    sun50i-a64-pinephone-1.2.dtb;sun50i-a64-pinephone.dtb \    
    Image \
    initramfs-uboot-image-pinephone.rootfs.cpio.gz.u-boot;initramfs-uboot-image-pinephone.uboot \
    boot.scr \
"

do_image_wic[depends] += "initramfs-uboot-image:do_image_complete"

PREFERRED_PROVIDER_virtual/kernel = "linux-pinephone"
KERNEL_IMAGETYPE = "Image"
KERNEL_DEVICETREE = "allwinner/sun50i-a64-pinephone-1.2.dtb"

SERIAL_CONSOLES = "115200;ttyS0;tty0"
MACHINE_FEATURES = "alsa apm keyboard rtc serial screen touchscreen usbgadget usbhost vfat ext2 ext3 wifi"
